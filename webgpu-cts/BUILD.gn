# Copyright 2021 The Chromium Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("webgpu-cts") {
  public_deps = [ ":compile_src" ]
}

input_ts_files = read_file("ts_sources.txt", "list lines")

ts_source_inputs = [ "src/tsconfig.json" ]
foreach(file, input_ts_files) {
  ts_source_inputs += [ "src/$file" ]
}

js_outputs = []
foreach(ts_file, input_ts_files) {
  js_file = string_replace(ts_file, ".ts", ".js")
  js_node_file = string_replace(js_file, "src/", "src-node/")

  js_outputs += [ "$target_gen_dir/$js_file" ]

  if (js_node_file != "src-node/common/runtime/wpt.js" &&
      js_node_file != "src-node/common/runtime/standalone.js" &&
      js_node_file != "src-node/common/runtime/helper/test_worker.js") {
    js_outputs += [ "$target_gen_dir/$js_node_file" ]
  }
}

action("compile_src") {
  script = "scripts/compile_src.py"

  inputs = [ "//third_party/node/node_modules/typescript/lib/tsc.js" ] +
           ts_source_inputs

  outputs = js_outputs
  data = js_outputs

  args = [ rebase_path("$target_gen_dir", root_build_dir) ]
}
