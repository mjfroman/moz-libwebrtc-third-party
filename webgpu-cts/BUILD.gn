# Copyright 2021 The Chromium Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("webgpu-cts") {
  public_deps = [
    ":compile_src",
    ":gen_listings",
  ]
}

input_ts_files = read_file("ts_sources.txt", "list lines")

ts_source_inputs = [ "src/tsconfig.json" ]
foreach(file, input_ts_files) {
  ts_source_inputs += [ "src/$file" ]
}

action("compile_src") {
  script = "scripts/compile_src.py"

  inputs = [ "//third_party/node/node_modules/typescript/lib/tsc.js" ] +
           ts_source_inputs
  js_files = []
  foreach(ts_file, input_ts_files) {
    js_file = string_replace(ts_file, ".ts", ".js")
    js_files += [ "$target_gen_dir/$js_file" ]
  }

  # Generated by action :gen_listings
  outputs = js_files - [ "$target_gen_dir/src/webgpu/listing.js" ]
  data = outputs

  args = [
    "--project",
    rebase_path("src/tsconfig.json", root_build_dir),
    "--outDir",
    rebase_path("$target_gen_dir/src", root_build_dir),
    "--noEmit",
    "false",
    "--declaration",
    "false",
    "--sourceMap",
    "false",
    "--target",
    "ES2017",
  ]
}

action("compile_gen_listings") {
  script = "scripts/compile_src.py"
  inputs = [ "//third_party/node/node_modules/typescript/lib/tsc.js" ] +
           ts_source_inputs
  outputs = [ "$target_gen_dir/node/tools/gen_listings.js" ]

  args = [
    rebase_path("src/src/common/tools/gen_listings.ts", root_build_dir),
    "--outDir",
    rebase_path("$target_gen_dir/node", root_build_dir),
    "--declaration",
    "false",
    "--sourceMap",
    "false",
  ]
}

action("gen_listings") {
  script = "//third_party/node/node.py"
  inputs = [ "$target_gen_dir/node/tools/gen_listings.js" ] + ts_source_inputs
  outputs = [ "$target_gen_dir/src/webgpu/listing.js" ]
  deps = [ ":compile_gen_listings" ]
  data = outputs

  # :compile_src generates a dummy version of webgpu/listing.js
  # Take a dependency on :compile_src so that :gen_listings always
  # runs after and overwrites it.
  deps += [ ":compile_src" ]

  args = [
    rebase_path("$target_gen_dir/node/tools/gen_listings.js", root_build_dir),
    "--no-validate",
    rebase_path("$target_gen_dir/src", root_build_dir),
    rebase_path("$target_gen_dir/src/webgpu", root_build_dir),
  ]
}
