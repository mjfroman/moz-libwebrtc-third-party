# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/libgav1/libgav1_srcs.gni")
import("//third_party/libgav1/options.gni")

config("libgav1_chromium_config") {
  include_dirs = [
    "src",
    "src/src",
  ]

  defines = [
    "LIBGAV1_MAX_BITDEPTH=10",
    "LIBGAV1_THREADPOOL_USE_STD_MUTEX",  # to avoid abseil dependency.
  ]
}

if (use_libgav1) {
  # Separate from libgav1 because utils/constants.cc and constants.cc generates
  # the same object file, constants.o.
  source_set("libgav1_utils") {
    configs -= [ "//build/config/compiler:chromium_code" ]
    configs += [
      "//build/config/compiler:no_chromium_code",
      ":libgav1_chromium_config",
    ]

    sources = gav1_utils_sources
  }

  static_library("libgav1") {
    configs -= [ "//build/config/compiler:chromium_code" ]
    configs += [
      "//build/config/compiler:no_chromium_code",
      ":libgav1_chromium_config",
    ]

    public_deps = [ ":libgav1_utils" ]

    # TODO(hiroh): Minimize source as small as obu parser can be built.
    sources = gav1_common_sources
    sources += gav1_dsp_sources
    sources += gav1_gav1_sources
    sources += gav1_tile_sources
  }
}
