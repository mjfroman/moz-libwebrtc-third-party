# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Template to generate wayland protocol code with wayland-scanner.
#
# Example:
#  wayland_protocol("foo") {
#    sources = [ "foo.xml" ]
#  }

import("//third_party/wayland/features.gni")

template("wayland_protocol") {
  assert(defined(invoker.sources), "Need sources for wayland protocol")

  # See the description of these confings below.
  is_code_marshalling = false
  is_wayland_protocol_server = false
  is_wayland_protocol_client = false
  if (defined(invoker.code_marshalling)) {
    is_code_marshalling = invoker.code_marshalling
  }
  if (defined(invoker.wayland_protocol_server)) {
    is_wayland_protocol_server = invoker.wayland_protocol_server
  }
  if (defined(invoker.wayland_protocol_client)) {
    is_wayland_protocol_client = invoker.wayland_protocol_client
  }

  assert((is_code_marshalling &&
          (!is_wayland_protocol_server && !is_wayland_protocol_client)) ||
         (is_wayland_protocol_server &&
          (!is_code_marshalling && !is_wayland_protocol_client)) ||
         (is_wayland_protocol_client &&
          (!is_code_marshalling && !is_wayland_protocol_server)) ||
         (!is_code_marshalling && !is_wayland_protocol_server &&
          !is_wayland_protocol_client))

  # Calculate the output paths.
  protocol_outputs = []
  output_dirs = []
  foreach(protocol, invoker.sources) {
    dir = "$root_gen_dir/" + rebase_path(get_path_info(protocol, "dir"), "//")
    name = get_path_info(protocol, "name")

    if (is_code_marshalling) {
      # If it's a protocol marshalling code, only public source code will be
      # generated.
      protocol_outputs += [ "${dir}/${name}-protocol.c" ]
    } else if (is_wayland_protocol_server) {
      # In case of server protocol generation, only server headers are generated.
      protocol_outputs += [
        "${dir}/${name}-server-protocol-core.h",
        "${dir}/${name}-server-protocol.h",
      ]
    } else if (is_wayland_protocol_client) {
      # In case of server protocol generation, only client headers are generated.
      protocol_outputs += [
        "${dir}/${name}-client-protocol-core.h",
        "${dir}/${name}-client-protocol.h",
      ]
    } else {
      # In all the other cases(generation of protocols from
      # //third_party/wayland-protocols), private source with server and client
      # headers are generated.
      protocol_outputs += [
        "${dir}/${name}-protocol.c",
        "${dir}/${name}-server-protocol.h",
        "${dir}/${name}-client-protocol.h",
      ]
    }
    output_dirs += [ dir ]
  }

  action_name = "${target_name}_gen"
  config_name = "${target_name}_config"
  source_set_name = "${target_name}"

  # Action which runs wayland-scanner to generate the code.
  action(action_name) {
    visibility = [ ":$source_set_name" ]
    script = "//third_party/wayland/wayland_scanner_wrapper.py"
    sources = invoker.sources
    outputs = protocol_outputs

    # Paths in invoker.sources are relative to the invoker.
    # Make it relative to the src root.
    args = rebase_path(invoker.sources, "//")

    args += [
      "--src-root",
      rebase_path("//", root_build_dir),
    ]
    args += [
      "--root-gen-dir",
      rebase_path(root_gen_dir, root_build_dir),
    ]

    generator_type = "all"
    if (is_code_marshalling) {
      generator_type = "protocol-marshalling"
    } else if (is_wayland_protocol_server) {
      generator_type = "protocol-server"
    } else if (is_wayland_protocol_client) {
      generator_type = "protocol-client"
    }
    args += [
      "--generator-type",
      generator_type,
    ]

    if (use_system_wayland_scanner) {
      args += [
        "--cmd",
        "./" + rebase_path(system_wayland_scanner_path, root_build_dir),
      ]
      sources += [ system_wayland_scanner_path ]
    } else {
      wayland_scanner_label =
          "//third_party/wayland:wayland_scanner($host_toolchain)"
      deps = [ wayland_scanner_label ]
      wayland_scanner_path = get_label_info(wayland_scanner_label,
                                            "root_out_dir") + "/wayland_scanner"
      args += [
        "--cmd",
        "./" + rebase_path(wayland_scanner_path, root_build_dir),
      ]
      sources += [ wayland_scanner_path ]
    }
  }

  # Config to include the generated headers only with the file names.
  # e.g. #include <foo-client-protocol.h>
  config(config_name) {
    include_dirs = output_dirs
  }

  # Source set which consists of the generated code.
  source_set(source_set_name) {
    sources = get_target_outputs(":$action_name")

    deps = [
      ":$action_name",
      "//third_party/wayland:wayland_util",
    ]

    configs -= [ "//build/config/compiler:chromium_code" ]
    configs += [ "//build/config/compiler:no_chromium_code" ]

    public_configs = [ ":$config_name" ]
  }
}
