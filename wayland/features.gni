# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/sysroot.gni")

declare_args() {
  # This variable is deprecated.  use_system_libwayland_client should be used
  # instead.
  use_system_libwayland = is_linux && !is_castos && !is_msan &&
                          default_toolchain != "//build/toolchain/cros:target"

  # Controls whether the build should use the version of Wayland
  # library shipped with the system or Chromium third_party (server library).
  use_system_libwayland_server = false
}

declare_args() {
  # Controls whether the build should use the version of Wayland
  # library shipped with the system or Chromium third_party (client library).
  use_system_libwayland_client = use_system_libwayland
}

declare_args() {
  # This may be set by Chromium packagers who do not wish to use the bundled
  # wayland scanner.
  use_system_wayland_scanner = use_system_libwayland_client
}

declare_args() {
  # Path to the wayland-scanner in the sysroot.
  assert(host_os == "linux")
  if (use_system_wayland_scanner) {
    if (use_sysroot) {
      system_wayland_scanner_path = "$sysroot/usr/bin/wayland-scanner"
    } else {
      system_wayland_scanner_path = "/usr/bin/wayland-scanner"
    }
  }
}
