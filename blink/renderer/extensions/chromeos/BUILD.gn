# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/blink/renderer/bindings/bindings.gni")
import("//third_party/blink/renderer/build/scripts/scripts.gni")
import("//third_party/blink/renderer/config.gni")
import("//third_party/blink/renderer/extensions/extensions.gni")

assert(use_blink_extensions_chromeos)

visibility = [
  "//third_party/blink/renderer/bindings/extensions/*",
  "//third_party/blink/renderer/extensions/*",
]

config("extensions_chromeos_implementation") {
  visibility += [ "//third_party/blink/renderer/bindings/extensions/*" ]
  defines = [ "BLINK_EXTENSIONS_CHROMEOS_IMPLEMENTATION=1" ]
}

component("chromeos") {
  output_name = "blink_extensions_chromeos"

  visibility = []  # Allow re-assignment of list.
  visibility = [ "//third_party/blink/*" ]

  sources = [
    "chromeos.cc",
    "chromeos.h",
    "chromeos_extensions.cc",
    "chromeos_extensions.h",
    "extensions_chromeos_export.h",
  ]

  # This sets the default list of configs when the blink_modules_sources target
  # is defined. The default_compiler_configs comes from BUILDCONFIG.gn and
  # is the list normally applied to static libraries and source sets.
  configs += [
    ":extensions_chromeos_implementation",

    "//third_party/blink/renderer:config",
    "//third_party/blink/renderer:non_test_config",
    "//third_party/blink/renderer:inside_blink",
  ]

  deps = [
    ":chromeos_events",
    "//third_party/blink/renderer/core",
    "//third_party/blink/renderer/extensions/chromeos/system_extensions/hid",
    "//third_party/blink/renderer/extensions/chromeos/system_extensions/window_management",
    "//third_party/blink/renderer/modules",
  ]

  public_deps =
      [ "//third_party/blink/renderer/bindings/extensions/v8:v8_chromeos" ]
}

group("make_chromeos_generated") {
  public_deps = [ ":make_chromeos_generated_event_target_names" ]
}

blink_chromeos_extensions_output_dir = "$blink_extensions_output_dir/chromeos"

# make_names -------------------------------------------------------------------
make_names("make_chromeos_generated_event_target_names") {
  in_files = [ "event_target_chromeos_names.json5" ]
  output_dir = blink_chromeos_extensions_output_dir
}

blink_extensions_chromeos_sources("chromeos_events") {
  sources = [ "event_target_chromeos.h" ]
  public_deps = [ ":chromeos_generated" ]
}

blink_extensions_chromeos_sources("chromeos_generated") {
  # Targets from above that generate outputs that need to be compiled.
  # All sources declared as outputs from these targets will be compiled
  # into one target.
  targets_generating_sources = [ ":make_chromeos_generated_event_target_names" ]

  sources = []
  foreach(current, targets_generating_sources) {
    sources += get_target_outputs(current)
  }
}
