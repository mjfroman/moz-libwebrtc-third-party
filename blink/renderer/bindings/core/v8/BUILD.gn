# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/libfuzzer/fuzzer_test.gni")
import("//third_party/blink/renderer/bindings/bindings.gni")
import("//third_party/blink/renderer/bindings/generated_in_core.gni")
import("//third_party/blink/renderer/bindings/scripts/scripts.gni")
import("//third_party/blink/renderer/core/core.gni")
import("//third_party/blink/renderer/core/core_idl_files.gni")

visibility = [ "//third_party/blink/renderer/*" ]

blink_core_sources("v8") {
  visibility = []
  visibility = [ "//third_party/blink/renderer/core" ]

  sources = generated_callback_function_sources_in_core +
            generated_callback_interface_sources_in_core +
            generated_dictionary_sources_in_core +
            generated_enumeration_sources_in_core +
            generated_interface_sources_in_core +
            generated_namespace_sources_in_core +
            generated_typedef_sources_in_core + generated_union_sources_in_core

  # Buffer source types generated by the old code generator
  sources += process_file_template(
          core_buffer_source_type_idl_files,
          [
            "$bindings_core_v8_output_dir/v8_{{source_name_part}}.cc",
            "$bindings_core_v8_output_dir/v8_{{source_name_part}}.h",
          ])

  deps = [
    ":generate_buffer_source_types",
    ":generated",
    "//third_party/blink/renderer/platform",
    "//v8",
  ]
}

source_set("testing") {
  testonly = true

  visibility = []
  visibility = [ "//third_party/blink/renderer/core/*" ]

  configs += [
    "//third_party/blink/renderer:config",
    "//third_party/blink/renderer:inside_blink",
  ]

  sources = generated_callback_function_sources_for_testing_in_core +
            generated_dictionary_sources_for_testing_in_core +
            generated_enumeration_sources_for_testing_in_core +
            generated_interface_sources_for_testing_in_core +
            generated_union_sources_for_testing_in_core

  deps = [
    ":generated",
    "//third_party/blink/renderer/core",
    "//third_party/blink/renderer/platform",
    "//v8",
  ]
}

group("generated") {
  visibility = []
  visibility = [
    "//third_party/blink/renderer/bindings/core/v8/*",
    "//third_party/blink/renderer/core/*",
  ]

  public_deps = [
    ":generate_buffer_source_types",
    "//third_party/blink/renderer/bindings:generate_bindings_all",
  ]
}

idl_compiler("generate_buffer_source_types") {
  sources = core_buffer_source_type_idl_files
  output_dir = bindings_core_v8_output_dir
  output_name_suffix = ""
  target_component = "core"
}

fuzzer_test("v8_serialized_script_value_fuzzer") {
  sources = [ "serialization/serialized_script_value_fuzzer.cc" ]
  seed_corpus = "serialization/fuzz_corpus"
  deps = [
    "//third_party/blink/public/common",
    "//third_party/blink/renderer/core",
    "//third_party/blink/renderer/core:testing",
    "//third_party/blink/renderer/platform:blink_fuzzer_test_support",
  ]
}
