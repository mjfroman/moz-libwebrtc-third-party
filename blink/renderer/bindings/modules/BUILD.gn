# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/blink/renderer/bindings/scripts/scripts.gni")

compute_interfaces_info("interfaces_info_modules") {
  sources = modules_static_interface_idl_files + modules_definition_idl_files +
            modules_static_dependency_idl_files +
            modules_generated_dependency_idl_files

  interfaces_info_file =
      "$bindings_modules_output_dir/interfaces_info_modules.pickle"
  component_info_file =
      "$bindings_modules_output_dir/component_info_modules.pickle"
  deps = [
    ":modules_global_constructors_idls",
    "//third_party/blink/renderer/bindings:global_constructors_idls",
  ]
}

compute_global_objects("modules_global_objects") {
  sources = modules_idl_files
  sources_generated = [ "$bindings_core_output_dir/global_objects_core.pickle" ]
  output_file = "$bindings_modules_output_dir/global_objects_modules.pickle"
  deps = [ "//third_party/blink/renderer/bindings/core:core_global_objects" ]
}

if (use_blink_v8_binding_new_idl_interface) {
  group("modules_global_constructors_idls") {
  }
} else {
  generate_global_constructors("modules_global_constructors_idls") {
    sources = modules_idl_files
    global_objects_file =
        "$bindings_modules_output_dir/global_objects_modules.pickle"
    interfaces = modules_global_constructors_original_interfaces
    basenames = modules_global_constructors_original_interface_basenames
    component = "modules"
    output_dir = blink_modules_output_dir
    deps = [ ":modules_global_objects" ]
  }
}
