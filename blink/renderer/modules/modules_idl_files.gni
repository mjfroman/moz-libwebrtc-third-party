# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/blink/renderer/bindings/bindings.gni")
import("//third_party/blink/renderer/config.gni")

# The paths in this file are absolute since this file is imported and the
# file lists must be valid from multple "current directories".

bindings_modules_output_dir = "$bindings_output_dir/modules"
blink_modules_output_dir = "$root_gen_dir/third_party/blink/renderer/modules"

modules_core_global_constructors_original_interfaces = [
  "AnimationWorkletGlobalScope",
  "AudioWorkletGlobalScope",
  "PaintWorkletGlobalScope",
  "ServiceWorkerGlobalScope",
]

# The size and the order in the following list must match to the previous one.
modules_core_global_constructors_original_interface_basenames = [
  "animation_worklet_global_scope",
  "audio_worklet_global_scope",
  "paint_worklet_global_scope",
  "service_worker_global_scope",
]

modules_global_constructors_original_interfaces = [
  "AnimationWorkletGlobalScope",
  "AudioWorkletGlobalScope",
  "DedicatedWorkerGlobalScope",
  "PaintWorkletGlobalScope",
  "ServiceWorkerGlobalScope",
  "SharedWorkerGlobalScope",
  "Window",
]

# The size and the order in the following list must match to the previous one.
modules_global_constructors_original_interface_basenames = [
  "animation_worklet_global_scope",
  "audio_worklet_global_scope",
  "dedicated_worker_global_scope",
  "paint_worklet_global_scope",
  "service_worker_global_scope",
  "shared_worker_global_scope",
  "window",
]

# The interfaces aren't technically files, but we can treat them as file names
# to get process_file_template to generate lists of IDL files corresponding
# to each interface.
modules_core_global_constructors_generated_idl_files = process_file_template(
        modules_core_global_constructors_original_interface_basenames,
        [ "$blink_modules_output_dir/{{source_name_part}}_core_constructors.idl" ])
modules_global_constructors_generated_idl_files = process_file_template(
        modules_global_constructors_original_interface_basenames,
        [ "$blink_modules_output_dir/{{source_name_part}}_modules_constructors.idl" ])

# Do not add anything to these lists. See _idl_imports below instead
# for the preferred mechanism.
modules_idl_files = []
modules_callback_function_idl_files = []

modules_dictionary_idl_files =
    get_path_info(
        [
          "app_banner/app_banner_prompt_result.idl",
          "app_banner/before_install_prompt_event_init.idl",
          "background_fetch/background_fetch_event_init.idl",
          "background_fetch/background_fetch_options.idl",
          "background_fetch/background_fetch_ui_options.idl",
          "background_sync/background_sync_options.idl",
          "background_sync/sync_event_init.idl",
          "background_sync/periodic_sync_event_init.idl",
          "bluetooth/bluetooth_advertising_event_init.idl",
          "bluetooth/bluetooth_le_scan_filter_init.idl",
          "bluetooth/bluetooth_le_scan_options.idl",
          "bluetooth/request_device_options.idl",
          "cache_storage/cache_query_options.idl",
          "cache_storage/multi_cache_query_options.idl",
          "canvas/canvas2d/canvas_rendering_context_2d_settings.idl",
          "canvas/canvas2d/hit_region_options.idl",
          "canvas/htmlcanvas/canvas_context_creation_attributes_module.idl",
          "clipboard/clipboard_item_options.idl",
          "contacts_picker/contact_info.idl",
          "contacts_picker/contacts_select_options.idl",
          "content_index/content_description.idl",
          "content_index/content_icon_definition.idl",
          "content_index/content_index_event_init.idl",
          "cookie_store/cookie_change_event_init.idl",
          "cookie_store/cookie_list_item.idl",
          "cookie_store/cookie_store_delete_options.idl",
          "cookie_store/cookie_store_get_options.idl",
          "cookie_store/cookie_store_set_extra_options.idl",
          "cookie_store/cookie_store_set_options.idl",
          "cookie_store/extendable_cookie_change_event_init.idl",
          "credentialmanager/authentication_extensions_client_inputs.idl",
          "credentialmanager/authentication_extensions_client_outputs.idl",
          "credentialmanager/authenticator_selection_criteria.idl",
          "credentialmanager/cable_authentication_data.idl",
          "credentialmanager/cable_registration_data.idl",
          "credentialmanager/collected_client_data.idl",
          "credentialmanager/credential_creation_options.idl",
          "credentialmanager/credential_data.idl",
          "credentialmanager/credential_request_options.idl",
          "credentialmanager/federated_credential_init.idl",
          "credentialmanager/federated_credential_request_options.idl",
          "credentialmanager/password_credential_data.idl",
          "credentialmanager/public_key_credential_creation_options.idl",
          "credentialmanager/public_key_credential_descriptor.idl",
          "credentialmanager/public_key_credential_entity.idl",
          "credentialmanager/public_key_credential_parameters.idl",
          "credentialmanager/public_key_credential_request_options.idl",
          "credentialmanager/public_key_credential_rp_entity.idl",
          "credentialmanager/public_key_credential_user_entity.idl",
          "csspaint/paint_rendering_context_2d_settings.idl",
          "device_orientation/device_motion_event_acceleration_init.idl",
          "device_orientation/device_motion_event_init.idl",
          "device_orientation/device_motion_event_rotation_rate_init.idl",
          "device_orientation/device_orientation_event_init.idl",
          "encoding/text_decode_options.idl",
          "encoding/text_decoder_options.idl",
          "encoding/text_encoder_encode_into_result.idl",
          "encryptedmedia/media_encrypted_event_init.idl",
          "encryptedmedia/media_key_message_event_init.idl",
          "encryptedmedia/media_key_system_configuration.idl",
          "encryptedmedia/media_key_system_media_capability.idl",
          "encryptedmedia/media_keys_policy.idl",
          "eventsource/event_source_init.idl",
          "filesystem/file_system_flags.idl",
          "gamepad/gamepad_axis_event_init.idl",
          "gamepad/gamepad_button_event_init.idl",
          "gamepad/gamepad_effect_parameters.idl",
          "gamepad/gamepad_event_init.idl",
          "geolocation/position_options.idl",
          "hid/hid_connection_event_init.idl",
          "hid/hid_device_filter.idl",
          "hid/hid_device_request_options.idl",
          "idle/idle_options.idl",
          "imagecapture/constrain_point_2d_parameters.idl",
          "imagecapture/photo_settings.idl",
          "imagecapture/point_2d.idl",
          "indexeddb/idb_database_info.idl",
          "indexeddb/idb_index_parameters.idl",
          "indexeddb/idb_object_store_parameters.idl",
          "indexeddb/idb_observer_init.idl",
          "indexeddb/idb_transaction_options.idl",
          "indexeddb/idb_version_change_event_init.idl",
          "installedapp/related_application.idl",
          "locks/lock_info.idl",
          "locks/lock_manager_snapshot.idl",
          "locks/lock_options.idl",
          "manifest/image_resource.idl",
          "media_capabilities/audio_configuration.idl",
          "media_capabilities/key_system_track_configuration.idl",
          "media_capabilities/media_configuration.idl",
          "media_capabilities/media_capabilities_decoding_info.idl",
          "media_capabilities/media_capabilities_info.idl",
          "media_capabilities/media_capabilities_key_system_configuration.idl",
          "media_capabilities/media_decoding_configuration.idl",
          "media_capabilities/media_encoding_configuration.idl",
          "media_capabilities/video_configuration.idl",
          "mediarecorder/blob_event_init.idl",
          "mediarecorder/media_recorder_options.idl",
          "mediasession/media_image.idl",
          "mediasession/media_metadata_init.idl",
          "mediasession/media_position_state.idl",
          "mediasession/media_session_action_details.idl",
          "mediasession/media_session_seek_to_action_details.idl",
          "native_file_system/choose_file_system_entries_options.idl",
          "native_file_system/choose_file_system_entries_options_accepts.idl",
          "native_file_system/file_system_create_writer_options.idl",
          "native_file_system/file_system_get_directory_options.idl",
          "native_file_system/file_system_get_file_options.idl",
          "native_file_system/file_system_handle_permission_descriptor.idl",
          "native_file_system/file_system_remove_options.idl",
          "native_file_system/get_system_directory_options.idl",
          "native_file_system/native_file_system_directory_iterator_entry.idl",
          "native_file_system/write_params.idl",
          "nfc/ndef_message_init.idl",
          "nfc/ndef_record_init.idl",
          "nfc/ndef_reading_event_init.idl",
          "nfc/ndef_scan_options.idl",
          "nfc/ndef_write_options.idl",
          "notifications/get_notification_options.idl",
          "notifications/notification_action.idl",
          "notifications/notification_event_init.idl",
          "notifications/notification_options.idl",
          "payments/address_errors.idl",
          "payments/android_pay_method_data.idl",
          "payments/android_pay_tokenization.idl",
          "payments/basic_card_request.idl",
          "payments/can_make_payment_event_init.idl",
          "payments/image_object.idl",
          "payments/merchant_validation_event_init.idl",
          "payments/payer_errors.idl",
          "payments/payment_address_init.idl",
          "payments/payment_currency_amount.idl",
          "payments/payment_details_base.idl",
          "payments/payment_details_init.idl",
          "payments/payment_details_modifier.idl",
          "payments/payment_details_update.idl",
          "payments/payment_handler_response.idl",
          "payments/payment_instrument.idl",
          "payments/payment_item.idl",
          "payments/payment_method_change_event_init.idl",
          "payments/payment_method_data.idl",
          "payments/payment_options.idl",
          "payments/payment_request_details_update.idl",
          "payments/payment_request_event_init.idl",
          "payments/payment_request_update_event_init.idl",
          "payments/payment_shipping_option.idl",
          "payments/payment_validation_errors.idl",
          "picture_in_picture/enter_picture_in_picture_event_init.idl",
          "picture_in_picture/picture_in_picture_options.idl",
          "presentation/presentation_connection_available_event_init.idl",
          "presentation/presentation_connection_close_event_init.idl",
          "push_messaging/push_event_init.idl",
          "push_messaging/push_subscription_change_event_init.idl",
          "push_messaging/push_subscription_options_init.idl",
          "scheduler/scheduler_post_task_options.idl",
          "sensor/sensor_error_event_init.idl",
          "sensor/sensor_options.idl",
          "sensor/spatial_sensor_options.idl",
          "shapedetection/barcode_detector_options.idl",
          "shapedetection/face_detector_options.idl",
          "shapedetection/landmark.idl",
          "sms/sms_receiver_options.idl",
          "storage/storage_event_init.idl",
          "webcodecs/video_decoder_init_parameters.idl",
          "webcodecs/video_track_writer_parameters.idl",
          "webmidi/midi_connection_event_init.idl",
          "webmidi/midi_message_event_init.idl",
          "webmidi/midi_options.idl",
          "webshare/share_data.idl",
          "websockets/close_event_init.idl",
          "websockets/websocket_close_info.idl",
          "websockets/websocket_connection.idl",
          "websockets/websocket_stream_options.idl",
          "webtransport/web_transport_close_info.idl",
          "webtransport/stream_abort_info.idl",
          "webusb/usb_connection_event_init.idl",
          "webusb/usb_control_transfer_parameters.idl",
          "webusb/usb_device_filter.idl",
          "webusb/usb_device_request_options.idl",
        ],
        "abspath")

# Do not add anything to these lists. See _idl_imports below instead
# for the preferred mechanism.

# 'partial interface' or target (right side of) 'implements'
modules_dependency_idl_files = []

# Similar, but for tests.
modules_testing_dependency_idl_files = []

_idl_imports = [
  "//third_party/blink/renderer/modules/accessibility/idls.gni",
  "//third_party/blink/renderer/modules/animationworklet/idls.gni",
  "//third_party/blink/renderer/modules/app_banner/idls.gni",
  "//third_party/blink/renderer/modules/audio_output_devices/idls.gni",
  "//third_party/blink/renderer/modules/background_fetch/idls.gni",
  "//third_party/blink/renderer/modules/background_sync/idls.gni",
  "//third_party/blink/renderer/modules/badging/idls.gni",
  "//third_party/blink/renderer/modules/battery/idls.gni",
  "//third_party/blink/renderer/modules/beacon/idls.gni",
  "//third_party/blink/renderer/modules/bluetooth/idls.gni",
  "//third_party/blink/renderer/modules/broadcastchannel/idls.gni",
  "//third_party/blink/renderer/modules/cache_storage/idls.gni",
  "//third_party/blink/renderer/modules/canvas/idls.gni",
  "//third_party/blink/renderer/modules/clipboard/idls.gni",
  "//third_party/blink/renderer/modules/compression/idls.gni",
  "//third_party/blink/renderer/modules/contacts_picker/idls.gni",
  "//third_party/blink/renderer/modules/content_index/idls.gni",
  "//third_party/blink/renderer/modules/cookie_store/idls.gni",
  "//third_party/blink/renderer/modules/credentialmanager/idls.gni",
  "//third_party/blink/renderer/modules/crypto/idls.gni",
  "//third_party/blink/renderer/modules/csspaint/idls.gni",
  "//third_party/blink/renderer/modules/device_orientation/idls.gni",
  "//third_party/blink/renderer/modules/donottrack/idls.gni",
  "//third_party/blink/renderer/modules/encoding/idls.gni",
  "//third_party/blink/renderer/modules/encryptedmedia/idls.gni",
  "//third_party/blink/renderer/modules/eventsource/idls.gni",
  "//third_party/blink/renderer/modules/filesystem/idls.gni",
  "//third_party/blink/renderer/modules/gamepad/idls.gni",
  "//third_party/blink/renderer/modules/geolocation/idls.gni",
  "//third_party/blink/renderer/modules/hid/idls.gni",
  "//third_party/blink/renderer/modules/idle/idls.gni",
  "//third_party/blink/renderer/modules/imagecapture/idls.gni",
  "//third_party/blink/renderer/modules/indexeddb/idls.gni",
  "//third_party/blink/renderer/modules/installedapp/idls.gni",
  "//third_party/blink/renderer/modules/keyboard/idls.gni",
  "//third_party/blink/renderer/modules/launch/idls.gni",
  "//third_party/blink/renderer/modules/locks/idls.gni",
  "//third_party/blink/renderer/modules/media_capabilities/idls.gni",
  "//third_party/blink/renderer/modules/mediacapturefromelement/idls.gni",
  "//third_party/blink/renderer/modules/mediarecorder/idls.gni",
  "//third_party/blink/renderer/modules/mediasession/idls.gni",
  "//third_party/blink/renderer/modules/mediasource/idls.gni",
  "//third_party/blink/renderer/modules/mediastream/idls.gni",
  "//third_party/blink/renderer/modules/native_file_system/idls.gni",
  "//third_party/blink/renderer/modules/navigatorcontentutils/idls.gni",
  "//third_party/blink/renderer/modules/netinfo/idls.gni",
  "//third_party/blink/renderer/modules/nfc/idls.gni",
  "//third_party/blink/renderer/modules/notifications/idls.gni",
  "//third_party/blink/renderer/modules/payments/idls.gni",
  "//third_party/blink/renderer/modules/peerconnection/idls.gni",
  "//third_party/blink/renderer/modules/permissions/idls.gni",
  "//third_party/blink/renderer/modules/picture_in_picture/idls.gni",
  "//third_party/blink/renderer/modules/plugins/idls.gni",
  "//third_party/blink/renderer/modules/presentation/idls.gni",
  "//third_party/blink/renderer/modules/push_messaging/idls.gni",
  "//third_party/blink/renderer/modules/quota/idls.gni",
  "//third_party/blink/renderer/modules/remoteplayback/idls.gni",
  "//third_party/blink/renderer/modules/scheduler/idls.gni",
  "//third_party/blink/renderer/modules/screen_enumeration/idls.gni",
  "//third_party/blink/renderer/modules/screen_orientation/idls.gni",
  "//third_party/blink/renderer/modules/sensor/idls.gni",
  "//third_party/blink/renderer/modules/service_worker/idls.gni",
  "//third_party/blink/renderer/modules/shapedetection/idls.gni",
  "//third_party/blink/renderer/modules/sms/idls.gni",
  "//third_party/blink/renderer/modules/speech/idls.gni",
  "//third_party/blink/renderer/modules/srcobject/idls.gni",
  "//third_party/blink/renderer/modules/storage/idls.gni",
  "//third_party/blink/renderer/modules/vibration/idls.gni",
  "//third_party/blink/renderer/modules/video_raf/idls.gni",
  "//third_party/blink/renderer/modules/wake_lock/idls.gni",
  "//third_party/blink/renderer/modules/webaudio/idls.gni",
  "//third_party/blink/renderer/modules/webcodecs/idls.gni",
  "//third_party/blink/renderer/modules/webdatabase/idls.gni",
  "//third_party/blink/renderer/modules/webgl/idls.gni",
  "//third_party/blink/renderer/modules/webgpu/idls.gni",
  "//third_party/blink/renderer/modules/webmidi/idls.gni",
  "//third_party/blink/renderer/modules/webshare/idls.gni",
  "//third_party/blink/renderer/modules/websockets/idls.gni",
  "//third_party/blink/renderer/modules/webtransport/idls.gni",
  "//third_party/blink/renderer/modules/webusb/idls.gni",
  "//third_party/blink/renderer/modules/xr/idls.gni",
]
if (!is_android) {
  _idl_imports += [ "//third_party/blink/renderer/modules/serial/idls.gni" ]
}

foreach(idl_import, _idl_imports) {
  # Avoid reassignment error by assigning to empty scope first.
  _imported = {
  }
  _imported = read_file(idl_import, "scope")

  # Paths are potentially relative to the location of the .gni. Rebase them
  # relative to "." so get_path_info() works as expected.
  gni_dir = get_path_info(idl_import, "dir")

  if (defined(_imported.modules_idl_files)) {
    modules_idl_files +=
        get_path_info(rebase_path(_imported.modules_idl_files, ".", gni_dir),
                      "abspath")
  }

  if (defined(_imported.modules_callback_function_idl_files)) {
    modules_callback_function_idl_files +=
        get_path_info(rebase_path(_imported.modules_callback_function_idl_files,
                                  ".",
                                  gni_dir),
                      "abspath")
  }

  if (defined(_imported.modules_dictionary_idl_files)) {
    modules_dictionary_idl_files +=
        get_path_info(
            rebase_path(_imported.modules_dictionary_idl_files, ".", gni_dir),
            "abspath")
  }

  if (defined(_imported.modules_dependency_idl_files)) {
    modules_dependency_idl_files +=
        get_path_info(
            rebase_path(_imported.modules_dependency_idl_files, ".", gni_dir),
            "abspath")
  }

  if (defined(_imported.modules_testing_dependency_idl_files)) {
    modules_testing_dependency_idl_files +=
        get_path_info(
            rebase_path(_imported.modules_testing_dependency_idl_files,
                        ".",
                        gni_dir),
            "abspath")
  }
}

modules_definition_idl_files = modules_dictionary_idl_files + modules_idl_files

# Static IDL files
modules_static_interface_idl_files = modules_callback_function_idl_files

modules_static_dependency_idl_files =
    modules_dependency_idl_files + modules_testing_dependency_idl_files

modules_generated_dependency_idl_files =
    modules_core_global_constructors_generated_idl_files +
    modules_global_constructors_generated_idl_files

# 'modules_dependency_idl_files' is already used in Source/modules, so avoid
# collision
modules_all_dependency_idl_files =
    modules_static_dependency_idl_files + modules_generated_dependency_idl_files
